plugins {
    id 'com.gradleup.shadow'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id "com.github.breadmoirai.github-release" version "2.4.1"
    id "com.modrinth.minotaur" version "2.+"
}

def platform = project.name;

architectury {
    platformSetupLoomIde()
    fabric()
}

fabricApi {
    configureDataGeneration() {
        client = true
        outputDirectory = project(':common').file('src/main/generated')
    }
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'Modrinth'
        url = 'https://api.modrinth.com/maven'
        content {
            includeGroup 'maven.modrinth'
        }
    }
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:$rootProject.fabric_loader_version"
    common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
    shadowBundle project(path: ':common', configuration: 'transformProductionFabric')

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:$rootProject.fabric_api_version"

    include(modImplementation("maven.modrinth:tiny-config:${project.tiny_config_version}-${platform}"))
}

processResources {
    inputs.property 'version', project.version

    filesMatching("fabric.mod.json") {
        expand(project.properties)
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    inputFile.set shadowJar.archiveFile
}

if(project.hasProperty('release_type')) {
    tasks.register("publishMod") {
        dependsOn(build)
        finalizedBy(tasks.curseforge)
        finalizedBy(tasks.modrinth)
        finalizedBy(tasks.githubRelease)
    }

    curseforge {
        apiKey = System.getenv("CURSEFORGE_API_KEY")
        project {
            id = System.getenv("CURSEFORGE_PROJECT_ID")
            changelog = changelog_last_section()
            changelogType = 'markdown'
            releaseType = release_type
            addGameVersion "${project.minecraft_version}"
            addGameVersion "${project.minecraft_compat_version}"
            addGameVersion "Fabric"

            mainArtifact(remapJar)
            afterEvaluate {
                uploadTask.dependsOn(remapJar)
            }
            options {
                forgeGradleIntegration = false
            }
        }
    }

    modrinth {
        token = System.getenv("MODRINTH_API_KEY")
        projectId = System.getenv("MODRINTH_PROJECT_ID") // This can be the project ID or the slug. Either will work!
        versionName = "${version}" // You don't need to set this manually. Will fail if Modrinth has this version already
        versionNumber = "${version}-${platform}" // You don't need to set this manually. Will fail if Modrinth has this version already
        versionType = release_type // This is the default -- can also be `beta` or `alpha`
        uploadFile = remapJar // With Loom, this MUST be set to `remapJar` instead of `jar`!
        gameVersions = ["${project.minecraft_version}", "${project.minecraft_compat_version}"] // Must be an array, even with only one version
        loaders = ["fabric"] // Must also be an array - no need to specify this if you're using Loom or ForgeGradle
        changelog = changelog_last_section()
        dependencies { // A special DSL for creating dependencies
            //required.project 'fabric-api'
        }
    }

    githubRelease {
        token System.getenv("GH_API_KEY") // This is your personal access token with Repo permissions
        owner System.getenv("GH_OWNER") // default is the last part of your group. Eg group: "com.github.breadmoirai" => owner: "breadmoirai"
        repo System.getenv("GH_REPO") // by default this is set to your project name
        tagName "${version}" // by default this is set to "v${project.version}"
        targetCommitish System.getenv("GH_BRANCH") // by default this is set to "main"
        releaseName "Release" // Release title, by default this is the same as the tagName
        generateReleaseNotes false // Generate release notes automatically, if true and body is present, body will be pre-pended, if name is not given, one will be generated by the tag
        // body.set "" // by default this is empty
        draft false // by default this is true
        prerelease release_type != "release" // by default this is false
        // releaseAssets jar.destinationDir.listFiles // this points to which files you want to upload as assets with your release, by default this is empty
        allowUploadToExisting false // Setting this to true will allow this plugin to upload artifacts to a release if it found an existing one. If overwrite is set to true, this option is ignored.
        overwrite false // by default false; if set to true, will delete an existing release with the same tag and name
        // dryRun true // by default false; you can use this to see what actions would be taken without making a release
        apiEndpoint "https://api.github.com" // should only change for github enterprise users
        client // This is the okhttp client used for http requests
    }
}